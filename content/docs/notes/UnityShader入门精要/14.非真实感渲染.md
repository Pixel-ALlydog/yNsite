---
date: 2024-03-6
title: 14、非真实感渲染
weight: 14
---

# 非真实渲染
非真实渲染（Non-Photorealistic Rendering, NPR）的一个主要目标是， 使用一些渲染方法使得画面达到和某些特殊绘画风格相似的效果如卡通、水彩风等。

## 卡通风格的渲染
使用这种风格的游戏画面通常有一些共有的特点，如物体都被黑色的线条描边，以及颜色分明的明暗变化等，实现卡通渲染的一种方法是基于色调的着色技术（tone-based shading）。在卡通风格中，模型的高光往往是一块块分界明显的纯色区域。

除了光照模型不同外，卡通风格通常还需要再物体边缘部分绘制轮廓。

### 渲染轮廓线
- 基于观察角度和表面法线的轮廓线渲染
- 过程式几何轮廓线渲染
- 基于图像处理的轮廓线渲染
- 基于轮廓边缘的轮廓线渲染
- 混合上述几种渲染的方法

**基于观察角度和表面法线的轮廓线渲染：**

使用视角方向和表面法线的点乘结果来得到轮廓线的信息；优点简单快速，在一个Pass中就能得到结果。缺点是很多模型描边效果不尽人意。

**过程式几何轮廓线渲染：**

核心使用两个Pass，第一个渲染背面的片段，并让其轮廓可见。第二个渲染正常片段；优点是快速有效，使用大多数表面平滑的模型。缺点是不适合类似立方体这样平整的模型。

**基于图像处理的轮廓线渲染：**

使用深度法线信息或屏幕颜色信息，在空间域上卷积；优点可以适用任何种类模型。缺点如一些深度和法线变化很小的轮廓无法被检测。

**基于轮廓边缘的轮廓线渲染：**

检测出精确的轮廓边，直接渲染。只需要检测这条边相邻的两个三角面片是否满足：
{{< katex display >}}
(n_0\cdot v > 0) != (n\cdot v > 0)
{{< /katex >}}
就认为该边为轮廓边。其中n0和n1为相邻三角面片的法向，v是从视角到边上任意顶点的方向。可以在几何着色器实现检测；优点是可以对边缘特殊处理，如水墨风格等。缺点是实现相对复杂，还有动画连贯问题，由于是逐帧单独提取轮廓在帧与帧之间会出现跳跃性。

**混合上述几种渲染的方法：**

如首先找到精确的轮廓，把模型和轮廓渲染到纹理中，再使用图像处理的方法识别轮廓，并在图像空间下进行风格化渲染。

这里使用基于图像处理的轮廓线渲染的方法。

### 高光
卡通风格高光往往是一块块分界明显的纯色区域，这里在Blinn Phong的基础上对高光部分进行阈值处理，同时需要对高光边缘区域做平滑处理，否则在高光边界区域会出现锯齿。
```shader
float spec = dot(worldNormal, worldHalfDir);
spec = step(threshold, spec);
float spec = dot(worldNormal, worldHalfDir);
spec = lerp(0,1, smoothstep(-w, w, spec - threshold));
```
{{< details title="catoon" open=true >}}
```shader
Shader "catoon/edge"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _Ramp("Ramp Texture", 2D) = "white"{}
        _Color("Color Tint", Color) = (1,1,1,1)
        _OutlineColor("Outline Color", Color) = (0,0,0,1)
        _Specular("Specular", Color) = (1,1,1,1)
        _Outline("Outline", Range(0,1)) = 0.1
        _SpecularScale("Specular Scale", Range(0,0.1))=0.01
    }
    SubShader{
        Tags{"RenderType" = "Opaque" "Queue" = "Geometry"}
        Pass{
            NAME "OUTLINE"
            Cull Front          //剔除正面
            CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			#include "UnityCG.cginc"
			
			float _Outline;
			fixed4 _OutlineColor;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			}; 
			struct v2f {
			    float4 pos : SV_POSITION;
			};
			v2f vert (a2v v) {
				v2f o;
				float4 pos = mul(UNITY_MATRIX_MV, v.vertex); 
				float3 normal = mul((float3x3)UNITY_MATRIX_IT_MV, v.normal);  
				normal.z = -0.5;
				pos = pos + float4(normalize(normal), 0) * _Outline;
				o.pos = mul(UNITY_MATRIX_P, pos);
				return o;
			}
			float4 frag(v2f i) : SV_Target { 
				return float4(_OutlineColor.rgb, 1);               
			}
			ENDCG
        }
        Pass{
            Tags{"LightMode" = "ForwardBase"}
            Cull Back
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
			#include "Lighting.cginc"
			#include "AutoLight.cginc"
            #pragma multi_compile_fwdbase

            sampler2D _MainTex;
            float4 _MainTex_ST;
            sampler2D _Ramp;
            float4 _Color;
            float4 _Specular;
            float _SpecularScale;

            struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 texcoord : TEXCOORD0;
			};
            struct v2f {
                float4 pos : POSITION;
                float2 uv : TEXCOORD0;
                float3 worldNormal : TEXCOORD1;
                float3 worldPos : TEXCOORD2;
                SHADOW_COORDS(3)
            };

            v2f vert(a2v v){
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);
                o.worldNormal=  mul(v.normal, (float3x3)unity_WorldToObject);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                TRANSFER_SHADOW(o);
                return o;
            }
            float4 frag(v2f i) : SV_Target{
                fixed3 worldNormal = normalize(i.worldNormal);
                fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
                fixed3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
                fixed3 worldHalfDir = normalize(worldLightDir + worldViewDir);

                UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
                fixed4 c = tex2D(_MainTex, i.uv);
                fixed3 albedo = c.rgb * _Color.rgb;
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed diff =  dot(worldNormal, worldLightDir);
                diff = (diff * 0.5 + 0.5) * atten;                                 //half lambert
                fixed3 diffuse = _LightColor0.rgb * albedo * tex2D(_Ramp, float2(diff, diff)).rgb;      //对渐变纹理取样作为diffuse颜色

                fixed spec = dot(worldNormal, worldHalfDir);
                fixed w = fwidth(spec) * 2.0;
                fixed3 specular = _Specular.rgb * lerp(0,1,smoothstep(-w, w, spec + _SpecularScale - 1)) * step(0.0001, _SpecularScale);
                return fixed4(ambient + diffuse + specular, 1.0);
            }
            ENDCG
        }
    }
    FallBack "Diffuse"
}
```
{{< /details >}}
## 素描风格的渲染
使用了提前生成的素描纹理来实现实时的素描风格渲染，这些纹理组成了一个色调艺术映射（Tonal Art Map，TAM）。
![](https://s2.loli.net/2024/03/06/Tw7uAOeXbtyV9Qv.png)
从左到右逐渐密集，从上到下类似mipmaps，但并不是简单的对上一层做降采样，而是需要保持笔触的间隔。

这里不考虑mipmaps的生成，直接使用6张素描纹理进行渲染。

首先在顶点着色器进行逐顶点光照，根据结果决定混合权重并传给片元着色器。之后再片元着色器中依照权重来混合6张采样纹理的结果。
{{< details title="TAM" open=true >}}
```shader
Shader "posteffect/TAM"
{
    Properties
    {
        _Color("Color Tint", Color) = (1,1,1,1)
        _TileFactor("Tile Factor", Float) = 1           //纹理平铺系数
        _Outline("Outline", Range(0, 1)) = 0.1
        _Hatch0("Hatch 0", 2D) = "white"{}              //6张素描纹理，线条密度依次增大
        _Hatch1("Hatch 1", 2D) = "white"{}
        _Hatch2("Hatch 2", 2D) = "white"{}
        _Hatch3("Hatch 3", 2D) = "white"{}
        _Hatch4("Hatch 4", 2D) = "white"{}
        _Hatch5("Hatch 5", 2D) = "white"{}
    }
    SubShader
    {
        Tags{"RenderType" = "Opaque" "Queue" = "Geometry"}
        UsePass "catoon/edge/OUTLINE"               //使用之前的描边
        Pass{
            Tags{"LightMode" = "ForwardBase"}
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma multi_compile_fwdbase
            #include "UnityCG.cginc"
			#include "Lighting.cginc"
			#include "AutoLight.cginc"

            struct a2v{
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f {
                float4 pos : SV_POSITION;
                float2 uv : TEXCOORD0;
                fixed3 hatchWeights0 : TEXCOORD1;
                fixed3 hatchWeights1 : TEXCOORD2;
                float3 worldPos : TEXCOORD3;
                SHADOW_COORDS(4)
            };
            float4 _Color;
            float _TileFactor;
            float _Outline;
            sampler2D _Hatch0;
            sampler2D _Hatch1;
            sampler2D _Hatch2;
            sampler2D _Hatch3;
            sampler2D _Hatch4;
            sampler2D _Hatch5;

            v2f vert(a2v v){
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = v.texcoord.xy * _TileFactor;

                fixed3 worldLightDir = normalize(WorldSpaceLightDir(v.vertex));
                fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
                fixed diff =  max(0,dot(worldLightDir, worldNormal));

                o.hatchWeights0 = fixed3(0,0,0);    //两个3维向量存储6张纹理权重
                o.hatchWeights1 = fixed3(0,0,0);

                float hatchFactor = diff * 7.0;     //权重因子，diff范围为[0, 1]
                if(hatchFactor > 6.0){              //diff越接近1则越亮，越接近0则越黑
                    //不用纹理
                }else if(hatchFactor > 5.0){
                    o.hatchWeights0.x = hatchFactor - 5.0;          //存储0纹理权重
                }else if(hatchFactor > 4.0){
                    o.hatchWeights0.x = hatchFactor - 4.0;          //存储0纹理权重
                    o.hatchWeights0.y = 1.0 - o.hatchWeights0.x;    //存储1纹理权重
                }else if(hatchFactor > 3.0){
                    o.hatchWeights0.y = hatchFactor - 3.0;          //存储1纹理权重
                    o.hatchWeights0.z = 1.0 - o.hatchWeights0.y;    //存储2纹理权重
                }else if(hatchFactor > 2.0){
                    o.hatchWeights0.z = hatchFactor - 2.0;          //存储2纹理权重
                    o.hatchWeights1.x = 1.0 - o.hatchWeights0.z;    //存储3纹理权重
                }else if(hatchFactor > 1.0){
                    o.hatchWeights1.x = hatchFactor - 1.0;          //存储3纹理权重
                    o.hatchWeights1.y = 1.0 - o.hatchWeights1.x;    //存储4纹理权重
                }else{
                    o.hatchWeights1.y = hatchFactor;                //存储4纹理权重
                    o.hatchWeights1.z = 1.0 - o.hatchWeights1.y;    //存储5纹理权重
                }
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                TRANSFER_SHADOW(o);
                return o;
            }
            fixed4 frag(v2f i) : SV_Target{
                fixed4 hatchTex0 = tex2D(_Hatch0, i.uv) * i.hatchWeights0.x;        //取样并乘以相应权重
                fixed4 hatchTex1 = tex2D(_Hatch1, i.uv) * i.hatchWeights0.y;
                fixed4 hatchTex2 = tex2D(_Hatch2, i.uv) * i.hatchWeights0.z;
                fixed4 hatchTex3 = tex2D(_Hatch3, i.uv) * i.hatchWeights1.x;
                fixed4 hatchTex4 = tex2D(_Hatch4, i.uv) * i.hatchWeights1.y;
                fixed4 hatchTex5 = tex2D(_Hatch5, i.uv) * i.hatchWeights1.z;

                //计算纯白在渲染中的贡献度，1减去6张权重剩下的就是白色  
                fixed4 whiteColor = fixed4(1,1,1,1) * (1-i.hatchWeights0.x - i.hatchWeights0.y - i.hatchWeights0.z - i.hatchWeights1.x - i.hatchWeights1.y - i.hatchWeights1.z);
                fixed4 hatchColor = hatchTex0 + hatchTex1 + hatchTex2 + hatchTex3 + hatchTex4 + hatchTex5 + whiteColor;     //混合颜色
                UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
                return fixed4(hatchColor.rgb * _Color.rgb * atten, 1.0);
            }
            ENDCG
        }
    }
    FallBack "DIFFUSE"
}
```
{{< /details >}}


