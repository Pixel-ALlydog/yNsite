---
date: 2024-03-6
title: 15、使用噪声
weight: 15
---

## 消融效果
可以使用噪声纹理+透明度测试实现。首先对噪声纹理的采样结果和某个控制消融程度的阈值比较，如果小于阈值，就使用clip函数把对应的像素裁剪掉，这些部分就对应被“消融”的区域。在镂空区域边缘使用两种颜色混合，再用pow函数处理后，与原纹理颜色混合后输出结果，达到不同的效果。
{{< details title="dissolve" open=true >}}
```shader
Shader "special effects/dissolve"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _BurnAmount("Burn Line Width", Range(0.0,1.0)) = 0.0            //消融程度
        _LineWidth("Burn Line Width", Range(0.0,0.2)) = 0.1             //控制边缘颜色
        _BumpMap("Normal Map", 2D) = "bump"{}                           
        _BurnFirstColor("Burn First Color", Color) = (1,0,0,1)          //边缘颜色1
        _BurnSecondColor("Burn Second Color", Color) = (1,0,0,1)        //边缘颜色2
        _BurnMap("Burn Map", 2D) = "white"{}                            //噪声
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" "Queue"="Geometry"}
        Pass{
            Tags{"LightMode" = "ForwardBase"}
            Cull Off                //关闭剔除，确保背面被渲染
            CGPROGRAM
            #include "UnityCG.cginc"
            #include "Lighting.cginc"
            #include "AutoLight.cginc"

            #pragma multi_compile_fwdbase
            #pragma vertex vert
		    #pragma fragment frag

            sampler2D _MainTex;
            float _BurnAmount;
            float _LineWidth;
            sampler2D _BumpMap;
            float4 _BurnFirstColor;
            float4 _BurnSecondColor;
            sampler2D _BurnMap;

            float4 _MainTex_ST;
            float4 _BumpMap_ST;
            float4 _BurnMap_ST;
            
            struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
                float4 tangent : TANGENT;
				float4 texcoord : TEXCOORD0;
			};
            struct v2f{
                float4 pos : SV_POSITION;
                float2 uvMainTex : TEXCOORD0;
                float2 uvBumpMap : TEXCOORD1;
                float2 uvBurnMap : TEXCOORD2;
                float3 lightDir : TEXCOORD3;
                float3 worldPos : TEXCOORD4;
                SHADOW_COORDS(5)
            };

            v2f vert(a2v v){
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);

                o.uvMainTex = TRANSFORM_TEX(v.texcoord, _MainTex);          //获取纹理坐标
                o.uvBumpMap = TRANSFORM_TEX(v.texcoord, _BumpMap);
                o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);

                TANGENT_SPACE_ROTATION;
                o.lightDir = mul(rotation, ObjSpaceLightDir(v.vertex)).xyz;         //光的方向从模型空间转为切线空间

                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;                 

                TRANSFER_SHADOW(o);
                return o;
            }
            fixed4 frag(v2f i) : SV_Target {
                fixed3 burn = tex2D(_BurnMap, i.uvBurnMap).rgb;

                clip(burn.r - _BurnAmount);                                         //若小于0则该像素被剔除
                float3 tangentLightDir = normalize(i.lightDir);
                fixed3 tangentNormal = UnpackNormal(tex2D(_BumpMap, i.uvBumpMap));
                
                fixed3 albedo =  tex2D(_MainTex, i.uvMainTex).rgb;
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(tangentLightDir, tangentNormal));

                fixed t = 1 - smoothstep(0.0, _LineWidth, burn.r - _BurnAmount);    //在宽度_lineWidth的范围内模拟颜色变化，t为1时表示该像素位于消融的边界，为0时为正常模型颜色
                fixed3 burnColor = lerp(_BurnFirstColor, _BurnSecondColor, t);      //两个颜色插值
                burnColor =  pow(burnColor,5);

                UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
                fixed3 finalColor = lerp(ambient + diffuse * atten, burnColor, t * step(0.0001, _BurnAmount));
                return fixed4(finalColor,1);
            }
            ENDCG
        }
        Pass{
            Tags{"LightMode" = "ShadowCaster"}                                      //阴影投射，对于使用透明度测试的物体阴影需要特别处理
            CGPROGRAM
            #pragma vertex vert
		    #pragma fragment frag
            #pragma multi_compile_shadowcaster                                      //指明编译指令
            #include "UnityCG.cginc"

            fixed _BurnAmount;
			sampler2D _BurnMap;
			float4 _BurnMap_ST;

            struct v2f {
				V2F_SHADOW_CASTER;                                                  //内置宏，会计算阴影投射需要的各种变量
				float2 uvBurnMap : TEXCOORD1;                   
			};

            v2f vert(appdata_base v){
                v2f o;
                TRANSFER_SHADOW_CASTER_NORMALOFFSET(o);                             //计算结果传给o中V2F_SHADOW_CASTER声明的变量
                o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);
                return o;                                                           //纹理
            }
            fixed4 frag(v2f i) : SV_Target{
                fixed3 burn =  tex2D(_BurnMap, i.uvBurnMap).rgb;                    //采样
                clip(burn.r - _BurnAmount);                                         //剔除
                SHADOW_CASTER_FRAGMENT(i)                                           //unity会完成阴影投射部分
            }
            ENDCG
        }
    }
}

```
{{< /details >}}

## 水波效果
使用一个由噪声纹理得到的法线贴图，实现一个包含菲涅尔反射的水面效果。这里使用cubmap作为环境纹理模拟反射，折射部分使用GrabPass来获取当前屏幕的渲染纹理，并使用切线空间下的法线方向对像素的屏幕坐标进行偏移，再使用该坐标对渲染纹理进行屏幕采样。水波的法线纹理由噪声纹理生成而得，对其进行随时间的偏移模拟波纹的效果。最后使用菲涅尔系数来动态决定混合系数。

{{< katex display >}}
f = pow(1-max(0, v\cdot n), 4) 
{{< /katex >}}



{{< details title="wave" open=true >}}
```shader
Shader "special effects/wave"
{
    Properties
    {
        _Color ("Main Color", Color) = (0,0.15,0.115,1)
        _MainTex ("Texture", 2D) = "white" {}
        _WaveMap("Wave Map", 2D) = "bump"{}                             //噪声
        _Cubemap("Cubemap", Cube) = "_Skybox"{}
        _WaveXSpeed("Wave Horizontal Speed", Range(-0.1,0.1)) = 0.01    //波纹x方向移动速度
        _WaveYSpeed("Wave Vertical Speed", Range(-0.1,0.1)) = 0.01      //波纹y方向移动速度
        _Distortion("Distortion", Range(0,100)) = 10                    //折射系数，控制水面扭曲程度
    }
    SubShader
    {
        Tags{"Queue" = "Transparent" "RenderType" = "Opaque"}
        GrabPass{"_RefractionTex"}                                      //定义字符串，获取屏幕图像
        Pass{
            Tags { "LightMode"="ForwardBase" }
            CGPROGRAM
            #pragma vertex vert;
            #pragma fragment frag;
            #include "UnityCG.cginc"
			#include "Lighting.cginc"
			#include "AutoLight.cginc"

            fixed4 _Color;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            sampler2D _WaveMap;
            float4 _WaveMap_ST;
            samplerCUBE _Cubemap;
            fixed _WaveXSpeed;
            fixed _WaveYSpeed;
            float _Distortion;
            sampler2D _RefractionTex;                                   //屏幕像素
            float4 _RefractionTex_TexelSize;

            struct a2v{
                float4 vertex : POSITION;
                float4 tangent : TANGENT;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f {
                float4 pos : SV_POSITION;
                float4 scrPos : TEXCOORD0;
                float4 uv : TEXCOORD1;
                float4 TtoW0 : TEXCOORD2;
                float4 TtoW1 : TEXCOORD3;
                float4 TtoW2 : TEXCOORD4;
            };

            v2f vert(a2v v)
            {
               v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				
				o.scrPos = ComputeGrabScreenPos(o.pos);
				
				o.uv.xy = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv.zw = TRANSFORM_TEX(v.texcoord, _WaveMap);
				
				float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;  
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);  
				fixed3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);  
				fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w;          //副切线
				
				o.TtoW0 = float4(worldTangent.x, worldBinormal.x, worldNormal.x, worldPos.x);  
				o.TtoW1 = float4(worldTangent.y, worldBinormal.y, worldNormal.y, worldPos.y);  
				o.TtoW2 = float4(worldTangent.z, worldBinormal.z, worldNormal.z, worldPos.z);  
				
				return o;
            }
            fixed4 frag(v2f i) : SV_Target{
                float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(worldPos));
				float2 speed = _Time.y * float2(_WaveXSpeed, _WaveYSpeed);                      //波纹速度
				
				fixed3 bump1 = UnpackNormal(tex2D(_WaveMap, i.uv.zw + speed)).rgb;              //切线空间下法线方向，这里是噪声纹理属性为法线
				fixed3 bump2 = UnpackNormal(tex2D(_WaveMap, i.uv.zw - speed)).rgb;              //采样两次，模拟两层交叉水面波动效果
				fixed3 bump = normalize(bump1 + bump2);
				
				float2 offset = bump.xy * _Distortion * _RefractionTex_TexelSize.xy;            //模拟折射偏移
				i.scrPos.xy = offset * i.scrPos.z + i.scrPos.xy;                                //扭曲屏幕像素位置
				fixed3 refrCol = tex2D( _RefractionTex, i.scrPos.xy/i.scrPos.w).rgb;            //折射颜色
				
				bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));    //切线空间转为世界空间，等于做矩阵乘法
				fixed4 texColor = tex2D(_MainTex, i.uv.xy + speed);                             //用偏移坐标采样
				fixed3 reflDir = reflect(-viewDir, bump);                                       //反射颜色，观察反向为该点指向摄像机
				fixed3 reflCol = texCUBE(_Cubemap, reflDir).rgb * texColor.rgb * _Color.rgb;    //环境映射
				
				fixed fresnel = pow(1 - saturate(dot(viewDir, bump)), 4);                       //菲涅尔系数
				fixed3 finalColor = reflCol * fresnel + refrCol * (1 - fresnel);                //混合
				
				return fixed4(finalColor, 1);
            }
            ENDCG
        }
    }
    FallBack Off
}

````
{{< /details >}}

![](https://s2.loli.net/2024/03/07/GlY6oxrg4zFZODf.png)


## 雾效
在之前雾效基础上，使用噪声纹理，对纹理偏移，达到不均匀雾效移动的效果。
```c#
...
//添加噪声纹理，传递相关参数
public Texture noiseTexture;
[Range(-0.5f, 0.5f)]
public float fogXspeed = 0.1f;
[Range(-0.5f, 0.5f)]
public float fogYspeed = 0.1f;
[Range(0.0f, 3.0f)]
public float noiseAmount = 1.0f;
...
void OnRenderImage{
    ...
    material.SetTexture("_NoiseTex", noiseTexture);
    material.SetFloat("_FogXSpeed", fogXspeed);
    material.SetFloat("_FogYSpeed", fogYspeed);
    material.SetFloat("_noiseAmount", noiseAmount);
    ...
}
...
```
```shader
...
_NoiseTex("noise tex", 2D) = "white"{}
_FogXSpeed("x speed", Float) = 0.1
_FogYSpeed("y speed", Float) = 0.1
_noiseAmount("noise amount", Float) = 1

fixed4 frag(v2f i) : SV_Target{
    ...
    float2 speed = _Time.y * float2(_FogXSpeed, _FogYSpeed);
    float noise = (tex2D(_NoiseTex, i.uv + speed).r - 0.5) * _noiseAmount;
    float fogDensity = (_FogEnd - worldPos.y) / (_FogEnd - _FogStart);
    fogDensity = saturate(fogDensity * _FogDensity * (1 + noise));
    fixed4 finalColor = tex2D(_MainTex, i.uv);
    finalColor.rgb = lerp(finalColor.rgb, _FogColor.rgb, fogDensity);
    ...
}
```