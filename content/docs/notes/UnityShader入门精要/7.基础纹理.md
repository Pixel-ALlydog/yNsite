---
date: 2024-02-25
title: 7、基础纹理
weight: 7
---
## 基础纹理
纹理映射（texture mapping），将一张图片贴在模型表面，逐纹素地控制模型的颜色。

纹理映射坐标（texture-mapping coordinates）定义了该顶点在纹理中对应的2D坐标，通常使用而一个二维变量表示（u，v），所以也称为UV坐标。

顶点UV坐标范围通常被归一化到[0, 1]。

Unity采用符合OpenGL的纹理空间，原点位于左下角。

通常会使用一张纹理来代替漫反射颜色。

在Blinn Phong光照下使用单张纹理：


{{< details title="texshader" open=true >}}
```shader
Shader "texture/texshader"
{
    Properties
    {
        _Color("Color Tint", Color) = (1,1,1,1)
        _Specular("Specular", Color) = (1,1,1,1)
        _Texture("Texture", 2D) = "white"{}
        _Gloss("Gloss", Range(8, 256)) = 20
    }
    SubShader
    {
        Pass
        {
            Tags{"LightMode" = "ForwardBase"}
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include"Lighting.cginc"

            float4 _Color;
            float _Gloss;
            sampler2D _Texture;
            float4 _Texture_ST;     //用来对纹理进行平移或缩放
            fixed4 _Specular;

            struct a2v
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };
            struct v2f
            {
                float4 pos : SV_POSITION;
                float3 worldNormal : TEXCOORD0;
                float3 worldPos : TEXCOORD1;
                float2 uv : TEXCOORD2;
            };

            v2f vert(a2v v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex);
                o.uv = v.texcoord.xy * _Texture_ST.xy + _Texture_ST.zw;      //纹理偏移或缩放
                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
                fixed3 worldNormal = normalize(i.worldNormal);
                fixed3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
                fixed3 halfDir = normalize(worldLightDir + viewDir);

                fixed3 albedo = tex2D(_Texture, i.uv).rgb * _Color.rgb;     //反照率，tex2D用来采样纹理

                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(worldNormal,worldLightDir));
                fixed3 specular = _LightColor0.rgb * _Specular.rgb * pow(max(0, dot(worldNormal, halfDir)), _Gloss);
                return fixed4(ambient + diffuse + specular, 1.0);
            }
            ENDCG
        }
    }
}
```
{{< /details >}}


### 纹理属性
1. Texture Type：有很多，如Normal map、Cubemap等等\
Alpha from Grayscale：如果勾选，则透明通道的值会由每个像素的灰度值生成\
2. Wrap Mode：决定了纹理坐标超过[0, 1]后会如何平铺，有几次模式:
- Repeat，会将整数部分舍弃，直接使用小数部分进行采样，结果是纹理会不断重复。
- Clamp：如果纹理大于1，则会截取到1，小于0，则截取到0。
3. Filter Mode：决定当纹理由于变换而产生拉伸时采取的滤波模式，支持3种模式：
- Point：使用最邻近插值法，
- Bilinear：线性滤波
- Trilinear：与Bilinear差不多，不过会与mipmapping进行混合
4. Generate Mip Mpas：多级渐远纹理，提前使用滤波处理来得到很多更小的图像，当相机比较远时，能快速得到结果。会多占用约33%的内存。
5. Max Size：若导入的纹理超过设置的值，Unity会缩放至设置大小，一般长宽为2的幂大小（Non Power of Two，NPOT）。
6. Format：决定Unity采用哪中格式存储该纹理。

## 凹凸映射（bump mapping）
两种主要方法：
- 高度映射：使用高度纹理（hight map）模拟表面位移（displacement），来得到修改后的法线值。
- 法线映射：使用法线纹理（normal map）
### 高度映射
hight map中存储的是迁都值（intensity），表示模型表面局部的海报高度。颜色越深则表面越凹。这种方法好处是直观，但计算更复杂。可以与法线纹理一起使用。
### 法线映射
法线纹理中存储表面发下的方向，范围在[-1, 1]，但图片像素的范围为[0, 1]，通常使用的映射为：
{{< katex display >}}
pixel = \frac{normal + 1}{2}
{{< /katex >}}
从图片映射回去：
{{< katex display >}}
normal = 2pixel - 1
{{< /katex >}}

法线存储的坐标空间有两种，对于模型自带的法线被定义在模型坐标空间（object-space normal map），另一种则使用模型顶点的切线空间（tangent space）来存储（tangent-space normal map）。

第一种看起是“五颜六色”的，第二种看起来则很蓝。

存储在模型空间下优点有：
- 实现简单，直观
- 可以提供平滑的边界

存储在切线空间下优点有：
- 自由度高，模型空间下记录的是绝对法线信息，仅可用于创建它的哪个模型，在其它模型上就完全错了；切线空间下记录的是相对法线信息。在不同网络上，也可以得到一个合理的结果。
- 可以进行UV动画，如移动一个纹理的UV来实现凹凸移动效果
- 可以重用法线纹理。
- 可压缩，切线空间下的法线Z方向总是正方向，所以可以仅存储XY方向，从而推出Z方向。

1. 切线空间下计算：
在切线空间变换，首先在顶点着色器中将光照方向和视角方向从模型空间变换到切线空间
，然后在片段着色器中，在切线空间下计算颜色值
{{< details title="tangentspace" open=true >}}
```shader
Shader "texture test/texshader"
{
    Properties
    {
        _Color("Color Tint", Color) = (1,1,1,1)
        _Specular("Specular", Color) = (1,1,1,1)
        _Gloss("Gloss", Range(8.0, 256)) = 20
        _MainTex("Main Tex", 2D) = "white"{}
        _BumpMap("Normal map", 2D) = "bump"{}       //法线纹理
        _BumpScale("Bump Scale", Float) = 1.0       //控制凹凸效果
    }
        SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "Lighting.cginc"
            #include "UnityCG.cginc"

            struct a2v {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 tangent : TANGENT;           //切线
                float4 texcoord : TEXCOORD0;        //传递纹理坐标
            };
            struct v2f {
                float4 pos : SV_POSITION;
                float4 uv : TEXCOORD0;              //xy存主要纹理坐标，zw存法线纹理坐标
                float3 lightDir : TEXCOORD1;
                float3 viewDir : TEXCOORD2;
            };
            fixed4 _Color;
            fixed4 _Specular;
            float _Gloss;
            sampler2D _MainTex;
            float4 _MainTex_ST;         //用于变换
            float4 _BumpMap_ST;         //用于变换
            sampler2D _BumpMap;
            float _BumpScale;

            v2f vert(a2v v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv.xy = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;
                o.uv.zw = v.texcoord.xy * _BumpMap_ST.xy + _BumpMap_ST.zw;
                //o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);

                float3 binormal = cross(normalize(v.normal), normalize(v.tangent.xyz)) * v.tangent.w;        //副切线，w决定了正负方向
                float3x3 rotation = float3x3(v.tangent.xyz, binormal, v.normal);                //变换矩阵，用于变换到切线空间
                //TRANGET_SPACE_ROTATION;

                o.lightDir = mul(rotation, ObjSpaceLightDir(v.vertex)).xyz;                     //变换到切线空间
                o.viewDir = mul(rotation, ObjSpaceViewDir(v.vertex)).xyz;                       //同理

                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                fixed3 tangentLightDir = normalize(i.lightDir);
                fixed3 tangentViewDir = normalize(i.viewDir);

                fixed4 packedNormal = tex2D(_BumpMap, i.uv.zw);             //采样
                fixed3 tangentNormal;

                //如果没有将纹理类型设置为法线纹理则需要将像素值从[0, 1]映射回[-1, 1]公式为normal = 2*pixel-1
                //这里已经设置，Unity会做好一切，使用UnpackNormal得到正确法线方向即可
                tangentNormal = UnpackNormal(packedNormal);                 
                tangentNormal.xy *= _BumpScale;                             //控制凹凸效果
                tangentNormal.z = sqrt(1.0 - saturate(dot(tangentNormal.xy, tangentNormal.xy)));        //通过xy计算z值
                fixed3 halfDir = normalize(tangentLightDir + tangentViewDir);

                fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;

                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(tangentNormal, tangentLightDir));
                fixed3 specular = _LightColor0.rgb * _Specular * pow(saturate(dot(halfDir, tangentNormal)), _Gloss);

                fixed3 color = ambient + diffuse + specular;
                return fixed4(color, 1.0);
            }

            ENDCG
        }
    }
}
```
{{< /details >}}
2. 世界空间下计算：在顶点着色其中计算从切线空间到世界空间的变换矩阵，并传递给片段着色器，之后在片段着色器将法线纹理转为世界坐标纹理：
{{< details title="worldspace" open=true >}}
```shader
Shader "Texture/worldSpace"
{
    Properties
    {
        _Color("Color Tint", Color) = (1,1,1,1)
        _Specular("Specular", Color) = (1,1,1,1)
        _Gloss("Gloss", Range(8.0, 256)) = 20
        _MainTex("Main Tex", 2D) = "white"{}
        _BumpMap("Normal map", 2D) = "bump"{}
        _BumpScale("Bump Scale", Float) = 1.0
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "Lighting.cginc"
            #include "UnityCG.cginc"
    
            struct a2v {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 tangent : TANGENT;       //切线
                float4 texcoord : TEXCOORD0;
            };
            struct v2f {
                float4 pos : SV_POSITION;
                float4 uv : TEXCOORD0;
                float4 TtoW0 : TEXCOORD1;
                float4 TtoW1 : TEXCOORD2;
                float4 TtoW2 : TEXCOORD3;

            };
            fixed4 _Color;
            fixed4 _Specular;
            float _Gloss;
            sampler2D _MainTex;
            float4 _MainTex_ST;
            float4 _BumpMap_ST;
            sampler2D _BumpMap;
            float _BumpScale;
    
            v2f vert(a2v v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv.xy = v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw;
                o.uv.zw = v.texcoord.xy * _BumpMap_ST.xy + _BumpMap_ST.zw;

                float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);
                fixed3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);
                fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w;

                o.TtoW0 = float4(worldTangent.x, worldBinormal.x, worldNormal.x, worldPos.x);
                o.TtoW1 = float4(worldTangent.y, worldBinormal.y, worldNormal.y, worldPos.y);
                o.TtoW2 = float4(worldTangent.z, worldBinormal.z, worldNormal.z, worldPos.z);   


                return o;
            }
    
            fixed4 frag(v2f i) : SV_Target
            {
                float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
                fixed3 lightDir = normalize(UnityWorldSpaceLightDir(worldPos));
                fixed3 viewDir = normalize(UnityWorldSpaceViewDir(worldPos));

                fixed3 bump = UnpackNormal(tex2D(_BumpMap, i.uv.zw));

                bump.xy *= _BumpScale;
                bump.z = sqrt(1.0 - saturate(dot(bump.xy, bump.xy)));

                bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));

                fixed3 halfDir = normalize(lightDir + viewDir);

                fixed3 albedo = tex2D(_MainTex, i.uv).rgb * _Color.rgb;
    
                fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
                fixed3 diffuse = _LightColor0.rgb * albedo * saturate(dot(bump, lightDir));
                fixed3 specular = _LightColor0.rgb * _Specular * pow(saturate(dot(halfDir, bump)), _Gloss);
    
                fixed3 color = ambient + diffuse + specular;
                return fixed4(color, 1.0);
            }
    
            ENDCG
        }
    }
}
```
{{< /details >}}

## 渐变纹理
一种常用的方法，用来控制漫反射光照的结果。可以实现一种插画风格的渲染效果，很多卡通风格的渲染都使用了该技术。

这种渐变纹理中的色调通常是突变的，即没有平滑过渡，以此模拟卡通中的阴影色块。

通常将纹理属性中的Wrap Mode设为Clamp模式，以防止对纹理采样时由于浮点数精度而造成问题。

使用Half Lambert光照模型，在片段着色器的diffuse中：
```shader
...
fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
fixed halfLambert = 0.5 * dot(worldNormal, worldLightDir) + 0.5;
fixed3 diffuseColor = tex2D(_RampTex, fixed2(halfLambert, halfLambert)).rgb * _Color.rgb;
fixed3 diffuse = _LightColor0.rgb * diffuseColor;
...
```

## 遮罩纹理
遮罩纹理（mask textrue），在某些区域控制光照效果，可以使用一张遮罩纹理来控制光照，也可以混合多张图片。

使用遮罩纹理的一般流程是：通过采样得到纹理的纹素值，然后使用其中某个（或某几个）通道的值与某种表面属性相乘。

在片段着色器specular中：
```shader
...
fixed specularMask = tex2D(_SpecularMask, i.uv).r * _SpecularScale;     //_SpecularScale用于控制遮罩影响的系数
fixed3 spcular = _LightColor0.rgb * _Specular.rgb * pow(max(0, dot(tangentNormal, halfDir)), _Gloss) * specularMask;
...
```






